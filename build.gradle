plugins {
  id 'java'
  id 'org.springframework.boot' version '3.5.10'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.siouan.frontend-jdk21' version '10.0.0'
}

group = 'io.github.wizwix'
version = '1.0.0'
description = 'kbpedia'

def frontendBuildDir = file("${projectDir}/frontend/dist")

java {
  toolchain.languageVersion = JavaLanguageVersion.of(21)
}

repositories {
  mavenCentral()
}

dependencies {
  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  runtimeOnly 'com.mysql:mysql-connector-j'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  // JWT (isn't part of Spring Boot ecosystem)
  implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
  // JSoup (sanitize user input, which is mix of CommonMark and HTML tags)
  implementation 'org.jsoup:jsoup:1.22.1'
}

configurations.compileOnly.extendsFrom(configurations.annotationProcessor)

processResources {
  dependsOn('copyFrontendToResources')

  def name = project.name
  def version = project.version.toString()

  filesMatching("**/banner.txt") { expand(application: [title: name, version: version]) }
}

frontend {
  packageJsonDirectory = file("${projectDir}/frontend")
  nodeVersion = '24.13.0'
  assembleScript = 'run build'
}

bootJar {
  dependsOn('copyFrontendToResources')
}

tasks.register('copyFrontendToResources', Copy) {
  dependsOn(assembleFrontend)
  from(frontendBuildDir)
  into("${project.layout.buildDirectory.get()}/resources/main/static")
}

springBoot { buildInfo() }

test { useJUnitPlatform() }
