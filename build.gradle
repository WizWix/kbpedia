plugins {
  id 'java'
  id 'org.springframework.boot' version '3.5.10'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.siouan.frontend-jdk21' version '10.0.0'
}

group = 'io.github.wizwix'
version = '1.0.0'
description = 'kbpedia'

def frontendBuildDir = file("${projectDir}/frontend/dist")

java {
  toolchain.languageVersion = JavaLanguageVersion.of(21)
}

repositories {
  mavenCentral()
}

dependencies {
  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  runtimeOnly 'com.mysql:mysql-connector-j'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

configurations.compileOnly.extendsFrom(configurations.annotationProcessor)

processResources {
  dependsOn('assembleFrontend')
  filesMatching("**/banner.txt") { expand(application: [title: project.name, version: project.version]) }
}

frontend {
  packageJsonDirectory = file("${projectDir}/frontend")
  nodeVersion = '24.13.0'
  assembleScript = 'run build'
}

springBoot { buildInfo() }

test { useJUnitPlatform() }
